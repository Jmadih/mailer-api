plugins {
  id 'application'
  id 'io.spring.dependency-management' version '1.0.4.RELEASE'
  id 'org.springframework.boot' version '1.5.9.RELEASE'
}

group = 'com.github.rlewan'
version = '0.0.1'

mainClassName = 'com.github.rlewan.mailer.Application'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
  options.compilerArgs << '-parameters'
}

def springBootVersion = plugins.getPlugin('org.springframework.boot').class.package.implementationVersion

repositories {
  jcenter()
}

def versions = [
  springfoxSwagger: '2.7.0',
  jackson: dependencyManagement.importedProperties['jackson.version'],
  springCloudHystrix: '1.4.2.RELEASE'
]

dependencies {
  compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-parameter-names', version: versions.jackson
  compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: versions.jackson

  compile group: 'com.mailjet', name: 'mailjet-client', version: '4.1.1'

  compile group: 'com.sendgrid', name: 'sendgrid-java', version: '4.0.1'

  compile group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
  compile group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger

  compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: springBootVersion
  compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springBootVersion

  compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-hystrix', version: versions.springCloudHystrix
  compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-hystrix-dashboard', version: versions.springCloudHystrix

  testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootVersion
}

task stage {
  dependsOn installDist
}
